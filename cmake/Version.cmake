#------------------------
# Project Version
#------------------------

# Retrieve version from header file
set(MIKAN_VERSION_HEADER_FILE "${ROOT_DIR}/src/Editor/AppCore/Version.h")

file(STRINGS ${MIKAN_VERSION_HEADER_FILE} MIKAN_VERSION_PARTS
  REGEX "^#define[ \t]+MIKAN_RELEASE_VERSION_(PRODUCT|MAJOR|MINOR|RELEASE)[ \t]+[0-9a-z]+$")
  
# Verify version parts
string(REGEX MATCH "MIKAN_RELEASE_VERSION_PRODUCT[ \t]+[0-9a-z]+" MIKAN_VERSION_PRODUCT  ${MIKAN_VERSION_PARTS})
string(REGEX MATCH "MIKAN_RELEASE_VERSION_MAJOR[ \t]+[0-9a-z]+" MIKAN_VERSION_MAJOR  ${MIKAN_VERSION_PARTS})
string(REGEX MATCH "MIKAN_RELEASE_VERSION_MINOR[ \t]+[0-9a-z]+" MIKAN_VERSION_MINOR  ${MIKAN_VERSION_PARTS})
string(REGEX MATCH "MIKAN_RELEASE_VERSION_RELEASE[ \t]+[0-9a-z]+" MIKAN_VERSION_RELEASE  ${MIKAN_VERSION_PARTS})

if(NOT MIKAN_VERSION_PRODUCT 
    OR NOT MIKAN_VERSION_MAJOR 
    OR NOT MIKAN_VERSION_MINOR 
    OR NOT MIKAN_VERSION_RELEASE)
  message(FATAL_ERROR "Unable to retrieve project version from ${MIKAN_VERSION_HEADER_FILE}")
endif()

# Extract version numbers
string(REGEX REPLACE "MIKAN_RELEASE_VERSION_PRODUCT[ \t]+([0-9a-z]+)" "\\1" MIKAN_VERSION_PRODUCT  ${MIKAN_VERSION_PRODUCT})
string(REGEX REPLACE "MIKAN_RELEASE_VERSION_MAJOR[ \t]+([0-9a-z]+)" "\\1" MIKAN_VERSION_MAJOR  ${MIKAN_VERSION_MAJOR})
string(REGEX REPLACE "MIKAN_RELEASE_VERSION_MINOR[ \t]+([0-9a-z]+)" "\\1" MIKAN_VERSION_MINOR  ${MIKAN_VERSION_MINOR})
string(REGEX REPLACE "MIKAN_RELEASE_VERSION_RELEASE[ \t]+([0-9a-z]+)" "\\1" MIKAN_VERSION_RELEASE  ${MIKAN_VERSION_RELEASE})

set(MIKAN_VERSION_STRING "${MIKAN_VERSION_PRODUCT}.${MIKAN_VERSION_MAJOR}_${MIKAN_VERSION_MINOR}.${MIKAN_VERSION_RELEASE}")

message(STATUS "Project version: ${MIKAN_VERSION_STRING}")
