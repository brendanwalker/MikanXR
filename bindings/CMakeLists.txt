set(ROOT_DIR ${CMAKE_CURRENT_LIST_DIR}/..)

if (POLICY CMP0122)
	cmake_policy(SET CMP0122 NEW)
endif()

include(${ROOT_DIR}/cmake/Environment.cmake)
include(${ROOT_DIR}/cmake/ThirdParty.cmake)

# Language bindings
file(MAKE_DIRECTORY ${CMAKE_BINARY_DIR}/bindings)

option(MIKAN_BUILD_CSHARP_BINDINGS "Build the C# bindings" ON)


# Language bindings (Python and C#)
if(SWIG_FOUND)
    include(${SWIG_USE_FILE})

    if(MIKAN_BUILD_CSHARP_BINDINGS)
        # MikanClient_swig_csharp runs SWIG to generate C# wrapper functions
        # Generated files are written to the bindings/csharp folder
        unset(CMAKE_SWIG_FLAGS)
        unset(CMAKE_SWIG_OUTDIR)
        set(CMAKE_SWIG_FLAGS -namespace "Mikan")        
        set(CMAKE_SWIG_OUTDIR "${CMAKE_CURRENT_LIST_DIR}/csharp")
    
        include_directories(${ROOT_DIR}/src/Client)
        swig_add_library(MikanClient_swig_csharp LANGUAGE csharp SOURCES ${CMAKE_CURRENT_LIST_DIR}/MikanClient.i)
        swig_link_libraries(MikanClient_swig_csharp Mikan_CAPI)
        set_target_properties(MikanClient_swig_csharp PROPERTIES FOLDER Client)
        set_target_properties(MikanClient_swig_csharp PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/csharp)
        set(INFO_BUILD_CSHARP_BININGS "Yes")
        install(TARGETS MikanClient_swig_csharp CONFIGURATIONS Debug DESTINATION ${MIKAN_DEBUG_INSTALL_PATH}/bin)
        install(TARGETS MikanClient_swig_csharp CONFIGURATIONS Release DESTINATION ${MIKAN_RELEASE_INSTALL_PATH}/bin)
        
        # MikanClient_csharp creates a C# assembly from the files generated by MikanClient_swig_csharp
        file(GLOB MIKANCLIENT_CSHARP_SRC
            "${CMAKE_CURRENT_LIST_DIR}/csharp/*.cs"
            "${CMAKE_CURRENT_LIST_DIR}/csharp/Properties/*.cs"
        )
        add_library(MikanClient_csharp SHARED ${MIKANCLIENT_CSHARP_SRC})
        add_dependencies(MikanClient_csharp MikanClient_swig_csharp)
        set_target_properties(MikanClient_csharp PROPERTIES FOLDER Client)
        set_target_properties(MikanClient_csharp PROPERTIES LINKER_LANGUAGE CSharp)
        install(TARGETS MikanClient_csharp 
            CONFIGURATIONS Debug 
            RUNTIME DESTINATION ${MIKAN_DEBUG_INSTALL_PATH}/bin
            LIBRARY DESTINATION ${MIKAN_DEBUG_INSTALL_PATH}/lib
            ARCHIVE DESTINATION ${MIKAN_DEBUG_INSTALL_PATH}/lib)
        install(TARGETS MikanClient_csharp 
            CONFIGURATIONS Release 
            RUNTIME DESTINATION ${MIKAN_RELEASE_INSTALL_PATH}/bin
            LIBRARY DESTINATION ${MIKAN_RELEASE_INSTALL_PATH}/lib
            ARCHIVE DESTINATION ${MIKAN_RELEASE_INSTALL_PATH}/lib)        
    else()
        set(INFO_BUILD_CSHARP_BINDINGS "No (disabled)")
    endif()
else()
    set(INFO_BUILD_CSHARP_BINDINGS "No (SWIG not found)")
endif()

message("")
message("  Language bindings")
message("    C#:               " ${INFO_BUILD_CSHARP_BINDINGS})
message("")